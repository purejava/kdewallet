plugins {
    id 'java-library'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api libs.com.github.hypfvieh.dbus.java.core
    api libs.com.github.hypfvieh.dbus.java.transport.native.unixsocket
    api libs.org.slf4j.slf4j.api
    testImplementation libs.org.junit.jupiter.junit.jupiter.api
    testImplementation libs.org.junit.jupiter.junit.jupiter.engine
    testImplementation libs.org.junit.jupiter.junit.jupiter
    testImplementation libs.org.slf4j.slf4j.simple
    testRuntimeOnly libs.org.junit.platform.junit.platform.launcher
}

group = 'org.purejava'
version = '1.6.1-SNAPSHOT'
description = 'A Java library for storing secrets on linux in a KDE wallet over D-Bus, implements kwallet.'
java.sourceCompatibility = JavaVersion.VERSION_19

def sonatypeUsername = System.getenv("SONATYPE_USERNAME") ?: ''
def sonatypePassword = System.getenv("SONATYPE_PASSWORD") ?: ''

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching "KDEWalletTest"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'keepassxc-proxy-access'
                description = 'A Java library for storing secrets on linux in a KDE wallet over D-Bus, implements kwallet.'
                url = 'https://github.com/purejava/kdewallet'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'purejava'
                        name = 'Ralph Plawetzki'
                        email = 'ralph@purejava.org'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/purejava/kdewallet.git'
                    developerConnection = 'scm:git:ssh://github.com/purejava/kdewallet.git'
                    url = 'https://github.com/purejava/kdewallet/tree/main'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/purejava/kdewallet/issues'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
}

if (!version.toString().endsWith("-SNAPSHOT")) {
    signing {
        useGpgCmd()
        sign configurations.runtimeElements
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.withType(Javadoc) {
    failOnError = false
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}
